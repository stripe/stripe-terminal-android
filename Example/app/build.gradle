apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

configurations {
    ktlint
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    buildToolsVersion = "28.0.3"
    compileSdkVersion = 28
    supportLibVersion = "28.0.0"

    appCompatVersion = '1.0.2'
    constraintVersion = '1.1.3'
    materialVersion = '1.0.0'
    legacySupportVersion = '1.0.0'
    okhttpVersion = '3.11.0'
    retrofitVersion = '2.5.0'
    stripeVersion = '7.1.0'
    stripeTerminalVersion = '1.0.0-b9'

    ktlintVersion = '0.33.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "com.google.android.material:material:$materialVersion"
    implementation "androidx.legacy:legacy-support-v4:$legacySupportVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintVersion"

    // OK HTTP
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Stripe Java library
    implementation "com.stripe:stripe-java:$stripeVersion"

    // Stripe Terminal library
    implementation "com.stripe:stripeterminal:$stripeTerminalVersion"

    // Ktlint
    ktlint "com.pinterest:ktlint:$ktlintVersion"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
