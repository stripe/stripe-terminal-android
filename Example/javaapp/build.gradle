plugins {
    id("com.android.application")
}

android {
    namespace = "com.stripe.example.javaapp"
    compileSdk = latestSdkVersion

    defaultConfig {
        minSdk = 26
        targetSdk = latestSdkVersion
        buildConfigField(
                "String",
                "EXAMPLE_BACKEND_URL",
                "\"${project.getProperty("EXAMPLE_BACKEND_URL").replaceAll("^[\"\\s]*|[\"\\s]*\$", "")}\""
        )
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
        buildConfig = true
    }

    lint {
        enable += "Interoperability"
        disable += "UnusedResources"
        disable += "UnknownNullness"
        disable += "MergeRootFrame"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

ext {
    androidx_lifecycle_version = "2.6.2"
    okhttp_version = "4.11.0"
    retrofit_version = "2.9.0"
    stripeTerminalVersion = "3.3.0"
}

dependencies {
    implementation(fileTree(dir: "libs", include: ["*.jar"]))

    implementation("com.google.android.material:material:1.9.0")
    implementation("androidx.activity:activity:1.7.2")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    // Annotations
    implementation("org.jetbrains:annotations:24.0.1")

    // ViewModel and LiveData
    implementation("androidx.lifecycle:lifecycle-livedata:$androidx_lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel:$androidx_lifecycle_version")

    // OK HTTP
    implementation("com.squareup.okhttp3:okhttp:$okhttp_version")

    // Retrofit
    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    implementation("com.squareup.retrofit2:converter-gson:$retrofit_version")

    // Stripe Terminal library
    implementation("com.stripe:stripeterminal:$stripeTerminalVersion")

    // Leak canary
    debugImplementation("com.squareup.leakcanary:leakcanary-android:2.12")
}
