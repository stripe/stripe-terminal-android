apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    compileSdkVersion rootProject.ext.maxSdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.maxSdkVersion
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    constraintVersion = '1.1.3'
    constraintlayoutVersion = '1.1.3'
    coreTestingVersion = '2.1.0'
    databindingVersion = '3.1.4'
    legacysupportVersion = '1.0.0'
    stripeTerminalVersion = '1.0.12'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "com.google.android.material:material:$rootProject.ext.material_version"
    implementation "androidx.legacy:legacy-support-v4:$legacysupportVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appcompat_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "com.facebook.stetho:stetho:$rootProject.ext.stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stetho_version"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.lifecycle_version"
    // We are using Kotlin, and not Java 8, so do not use the recommended Java 8 library
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.ext.lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"

    // Databinding
    kapt "com.android.databinding:compiler:$databindingVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.kotlin_coroutines_version"

    // OK HTTP
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttp_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit_version"

    // Stripe Terminal library
    implementation "com.stripe:stripeterminal:$stripeTerminalVersion"

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.leakcanary_version"

    // Ktlint
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
