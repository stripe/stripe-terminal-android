plugins {
    id("com.android.application")
    id("kotlin-android")
    id("org.jetbrains.kotlin.plugin.parcelize")
    id("kotlin-kapt")
}

android {
    namespace = "com.stripe.example"
    compileSdk = latestSdkVersion

    defaultConfig {
        minSdk = 26
        targetSdk = latestSdkVersion
        buildConfigField(
                "String",
                "EXAMPLE_BACKEND_URL",
                "\"${project.getProperty("EXAMPLE_BACKEND_URL").replaceAll("^[\"\\s]*|[\"\\s]*\$", "")}\""
        )
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
        buildConfig = true
    }

    lint {
        disable += "IconLauncherShape"
        disable += "MergeRootFrame"
        disable += "TypographyEllipsis"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

// Force Kapt task to use Java 8. See https://youtrack.jetbrains.com/issue/KT-55947.
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KaptGenerateStubs).configureEach {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

ext {
    androidx_lifecycle_version = "2.6.2"
    kotlin_coroutines_version = "1.7.3"
    retrofit_version = "2.9.0"
    stripeTerminalVersion = "3.4.0"
}

dependencies {
    implementation(fileTree(dir: "libs", include: ["*.jar"]))

    implementation("com.google.android.material:material:1.11.0")
    implementation("androidx.activity:activity-ktx:1.8.2")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("androidx.core:core-ktx:1.12.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")

    // ViewModel and LiveData
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version")

    // OK HTTP
    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    // Retrofit
    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    implementation("com.squareup.retrofit2:converter-gson:$retrofit_version")

    // Stripe Terminal library
    implementation("com.stripe:stripeterminal:$stripeTerminalVersion")

    // Leak canary
    debugImplementation("com.squareup.leakcanary:leakcanary-android:2.13")
}
