apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    buildToolsVersion = "29.0.0"
    compileSdkVersion = 29
    supportLibVersion = "28.0.0"

    appcompatVersion = '1.1.0'
    constraintVersion = '1.1.3'
    constraintlayoutVersion = '1.1.3'
    coreTestingVersion = '2.1.0'
    coroutineVersion = '1.3.2'
    databindingVersion = '3.1.4'
    legacysupportVersion = '1.0.0'
    materialVersion = '1.0.0'
    okhttpVersion = '4.2.2'
    retrofitVersion = '2.6.2'
    stethoVersion = '1.5.1'
    viewModelVersion = '2.1.0'

    leakCanaryVersion = '2.0-beta-3'

    stripeTerminalVersion = '1.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "com.google.android.material:material:$materialVersion"
    implementation "androidx.legacy:legacy-support-v4:$legacysupportVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$viewModelVersion"
    // We are using Kotlin, and not Java 8, so do not use the recommended Java 8 library
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:$viewModelVersion"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"

    // Databinding
    kapt "com.android.databinding:compiler:$databindingVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // OK HTTP
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Stripe Terminal library
    implementation "com.stripe:stripeterminal:$stripeTerminalVersion"

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    // Ktlint
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
